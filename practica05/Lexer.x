{
module Lexer (Token(..), lexer) where

import Data.Char (isSpace)
}

%wrapper "basic"

$white = [\x20\x09\x0A\x0D\x0C\x0B]
$digit = 0-9
$nonzero = 1-9
$letter = [A-Za-z_]
$idrest = [A-Za-z0-9_]

@nat = 0 | $nonzero $digit*

tokens :-

  $white+               ;

  \(                    { \_ -> TokenPA }
  \)                    { \_ -> TokenPC }
  \+                    { \_ -> TokenSuma }
  \-                    { \_ -> TokenResta }
  not                   { \_ -> TokenNot }
  letrec                { \_ -> TokenLetRec}
  let\*                 { \_ -> TokenLetStar }
  let                   { \_ -> TokenLet }
  lambda                { \_ -> TokenLambda }

  if                    { \_ -> TokenIf}
  cond                  { \_ -> TokenCond}
  else                  { \_ -> TokenElse}

  "#t"                  { \_ -> TokenBool True }
  "#f"                  { \_ -> TokenBool False }

  0$digit+              { \s -> error ("Lexical error: natural con cero inicial = "
                                      ++ show s) }
  @nat                  { \s -> TokenNum (read s) }

  $letter$idrest*       { \s -> TokenId s }

  .                     { \s -> error ("Lexical error: caracter no reconocido = "
                                      ++ show s
                                      ++ " | codepoints = "
                                      ++ show (map fromEnum s)) }

{
data Token
  = TokenId String
  | TokenNum Int
  | TokenBool Bool
  | TokenSuma
  | TokenResta
  | TokenNot
  | TokenLet
  | TokenLetStar
  | TokenLambda
  | TokenIf
  | TokenCond
  | TokenElse
  | TokenLetRec
  | TokenPA
  | TokenPC
  deriving (Eq, Show)

normalizeSpaces :: String -> String
normalizeSpaces = map (\c -> if isSpace c then '\x20' else c)

lexer :: String -> [Token]
lexer = alexScanTokens . normalizeSpaces
}
